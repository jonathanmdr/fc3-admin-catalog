buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.flywaydb:flyway-mysql:10.11.0")
    }
}

plugins {
    id "java"
    id "application"
    id "org.springframework.boot" version "3.2.4"
    id "io.spring.dependency-management" version "1.1.4"
    id "org.flywaydb.flyway" version "10.11.0"
}

group "org.fullcycle.admin.catalog.infrastructure"
version "1.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

bootJar {
    archiveFileName = "application.jar"
    into("libs") {
        from(sourceSets.main.output)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation("io.vavr:vavr:0.10.4")

    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation("org.springframework.boot:spring-boot-starter-undertow")

    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0')

    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    runtimeOnly("com.mysql:mysql-connector-j")

    implementation("org.flywaydb:flyway-core:10.11.0")
    implementation("org.flywaydb:flyway-mysql:10.11.0")

    implementation 'com.google.cloud:google-cloud-storage:2.36.1'
    implementation 'com.google.guava:guava:33.1.0-jre'

    testImplementation(project(path: ':domain', configuration: 'testClasses'))

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.flywaydb:flyway-core")

    testImplementation("org.testcontainers:testcontainers:1.19.7")
    testImplementation("org.testcontainers:mysql:1.19.7")
    testImplementation("org.testcontainers:junit-jupiter:1.19.7")

    testRuntimeOnly("com.h2database:h2")
}

flyway {
    url = System.getenv("FLYWAY_DB") ?: "jdbc:mysql://localhost:3306/admin_catalog_videos"
    user = System.getenv("FLYWAY_USER") ?: "root"
    password = System.getenv("FLYWAY_PASS") ?: "root"
}

test {
    filter {
        includeTestsMatching("*Test")
        includeTestsMatching("*IT")
    }
    useJUnitPlatform()
}